pkgname="ytmediatool"
pkgver="0.5.1"
pkgrel="1"
pkgdesc="Tool to download large and small quantities of video and audio files"
arch=('any')
url="https://github.com/Saju159/YTMediaTool/"
license=("GPL-3.0")
depends=("ffmpeg" "python" "pyside6" "yt-dlp" "python-mutagen" "python-ytmusicapi" "python-spotipy")
makedepends=("python-build" "python-installer" "python-wheel")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/Saju159/YTMediaTool/archive/refs/tags/v${pkgver}.tar.gz" "YTMediaTool.desktop")
sha256sums=("99a8657b822aec8d87385cc99a686594948efc49dc42eddad1dd1f1a80dcc8be" "SKIP")

build() {
	cd "YTMediaTool-${pkgver}"
	python -m build --wheel --no-isolation
}

package() {
	cd "YTMediaTool-${pkgver}"
	python -m installer --destdir="$pkgdir" dist/*.whl
	install -vDm 755 ../YTMediaTool.desktop "${pkgdir}/usr/share/applications/YTMediaTool.desktop"
}
